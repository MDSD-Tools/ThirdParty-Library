plugins {
	id 'org.standardout.bnd-platform' version "1.7.0"	
}

repositories {
	mavenCentral()
}

def spiflyVersion = "1.2.3"
def guiceVersion = "4.2.2"
def guiceGuavaVersion = "25.1-jre"

defaultTasks 'clean', 'bundles', 'patchSpiFlyP2Inf', 'copyPatchedBundle', 'updateSite'

platform {
	useBndHashQualifiers = false
	useFeatureHashQualifiers = false
	eclipseMirror = "http://ftp.halifax.rwth-aachen.de/eclipse/technology/epp/downloads/release/2019-06/R/eclipse-java-2019-06-R-linux-gtk-x86_64.tar.gz"
	
	feature(id: "org.apache.aries.spifly.dynamic.feature", name: "Apache Aries SPI Fly Dynamic Bundle", version: "$spiflyVersion") {
		bundle 'org.apache.aries:org.apache.aries.util:1.1.1'
		bundle "org.apache.aries.spifly:org.apache.aries.spifly.dynamic.bundle:$spiflyVersion"
	}

	feature(id: "com.google.inject.guice.feature", name: "Google Guice Feature", version: "$guiceVersion") {
		plugin "com.google.inject:guice:$guiceVersion:no_aop" , {
			exclude group: 'aopalliance', module: 'aopalliance'
			exclude group: 'javax.inject', module: 'javax.inject'
			exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
			exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
			exclude group: 'com.google.code.findbugs', module: 'jsr305'
			exclude group: 'org.checkerframework', module: 'checker-qual'
			exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
		}
		plugin "com.google.guava:guava:$guiceGuavaVersion" , {
			exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
			exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
			exclude group: 'com.google.code.findbugs', module: 'jsr305'
			exclude group: 'org.checkerframework', module: 'checker-qual'
			exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
		}
	}
}

task patchSpiFlyP2Inf(type: Zip) {
	archiveFileName = "org.apache.aries.spifly.dynamic.bundle_${spiflyVersion}.jar"
	destinationDirectory = file("build/tmp/plugins")
	from zipTree("build/plugins/org.apache.aries.spifly.dynamic.bundle_${spiflyVersion}.jar")
	from zipTree("org.apache.aries.spifly.dynamic.bundle.p2inf.patch.jar")
}

task copyPatchedBundle(type: Copy) {
	from file("build/tmp/plugins/org.apache.aries.spifly.dynamic.bundle_${spiflyVersion}.jar")
	into file("build/plugins/")
}